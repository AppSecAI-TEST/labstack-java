group 'com.labstack'
version '0.0.1'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.8

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.squareup.okhttp3:okhttp:3.8.1'
    compile 'com.squareup.moshi:moshi:1.5.0'
    testCompile 'junit:junit:4.12'
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'labstack-java'
                packaging 'jar'
                description 'Official Java client library for the LabStack web API'
                url 'https://labstack.com'

                scm {
                    url 'scm:git@github.com:labstack/labstack-java.git'
                    connection 'scm:git@github.com:labstack/labstack-java.git'
                    developerConnection 'scm:git@github.com:labstack/labstack-java.git'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'http://opensource.org/licenses/MIT'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'vishr'
                        name 'Vishal Rana'
                    }
                }
            }
        }
    }
}

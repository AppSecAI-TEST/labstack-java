group 'com.labstack'
version '0.5.3'

apply plugin: 'java'
apply plugin: 'maven-publish'

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.squareup.okhttp3:okhttp:3.8.1'
    compile 'com.squareup.moshi:moshi:1.5.0'
    compile 'com.squareup.moshi:moshi-adapters:1.5.0'
    testCompile 'junit:junit:4.12'
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar
            artifact javadocJar

            pom.withXml {
                asNode().with() {
                    appendNode('name', 'labstack-java')
                    appendNode('description', 'Official Java client library for the LabStack REST API')
                    appendNode('url', 'https://labstack.com')
                    appendNode('scm').with {
                        appendNode('url', 'https://github.com/labstack/labstack-java.git')
                    }
                    appendNode('developers').with {
                        appendNode('developer').with {
                            appendNode('id', 'vishr')
                            appendNode('name', 'Vishal Rana')
                        }
                    }
                    appendNode('licenses').with {
                        appendNode('license').with {
                            appendNode('name', 'MIT License')
                            appendNode('url', 'https://github.com/labstack/labstack-java/blob/master/LICENSE')
                        }
                    }
                }
            }
        }
    }
}

